set(PACKAGE_VERSION "@LeapHTTP_VERSION@")
set(PACKAGE_VERSION_MAJOR "@LeapHTTP_VERSION_MAJOR@")
set(PACKAGE_VERSION_MINOR "@LeapHTTP_VERSION_MINOR@")
set(PACKAGE_VERSION_PATCH "@LeapHTTP_VERSION_PATCH@")

if(LeapHTTP_DEBUG)
  message(STATUS "Debug mode on")
  message(STATUS "Installed LeapHTTP_VERSION: ${PACKAGE_VERSION}")

  message(STATUS "Installed LeapHTTP_BUILD_ARCHITECTURES: @LeapHTTP_BUILD_ARCHITECTURES@")
  message(STATUS "Configured LeapHTTP_BUILD_ARCHITECTURES: ${LeapHTTP_BUILD_ARCHITECTURES}")

  message(STATUS "Installed CMAKE_SIZEOF_VOID_P: @CMAKE_SIZEOF_VOID_P@")
  message(STATUS "Configured CMAKE_SIZEOF_VOID_P: ${CMAKE_SIZEOF_VOID_P}")
endif()

# If the customer has an override architecture requirement, use that
if(DEFINED LeapHTTP_ARCHITECTURE)
  foreach(i IN LISTS LeapHTTP_ARCHITECTURE)
    string(REGEX MATCH "amd64|x86_64|x64" LeapHTTP_is_x64 ${i})
    string(REGEX MATCH "i386|x86" LeapHTTP_is_x86 ${i})
    string(REGEX MATCH "arm" LeapHTTP_is_arm ${i})

    # Classify
    if(LeapHTTP_is_x86)
      list(APPEND LeapHTTP_ARCHITECTURES x86)
    elseif(LeapHTTP_is_x64)
      list(APPEND LeapHTTP_ARCHITECTURES x64)
    elseif(LeapHTTP_is_arm)
      list(APPEND LeapHTTP_ARCHITECTURES arm)
    else()
      message(WARNING "Unrecognized architecture ${i}")
    endif()
  endforeach()
else()
  # Try to infer what the user wants
  if(CMAKE_SYSTEM_PROCESSOR STREQUAL "arm")
    set(LeapHTTP_ARCHITECTURES arm)
  elseif(CMAKE_SIZEOF_VOID_P EQUAL "8")
    set(LeapHTTP_ARCHITECTURES x64)
  else()
    set(LeapHTTP_ARCHITECTURES x86)
  endif()
endif()

if(LeapHTTP_DEBUG)
  message(STATUS "LeapHTTP_ARCHITECTURE: ${LeapHTTP_ARCHITECTURE}")
  message(STATUS "LeapHTTP_ARCHITECTURES: ${LeapHTTP_ARCHITECTURES}")
endif()

# Architectures we were built with
set(LeapHTTP_BUILD_ARCHITECTURES @LeapHTTP_BUILD_ARCHITECTURES@)
if(LeapHTTP_DEBUG)
  message(STATUS "LeapHTTP_BUILD_ARCHITECTURES: ${LeapHTTP_BUILD_ARCHITECTURES}")
endif()

# Verify that we match all of the architectures requested by the customer
foreach(i IN LISTS LeapHTTP_ARCHITECTURES)
  list(FIND LeapHTTP_BUILD_ARCHITECTURES ${i} found)
  if(found EQUAL -1)
    set(PACKAGE_VERSION_COMPATIBLE FALSE)
    set(PACKAGE_VERSION_UNSUITABLE TRUE)
    if(LeapHTTP_DEBUG)
      message(STATUS "Requested architecture of ${LeapHTTP_ARCHITECTURE} not compatible with @LeapHTTP_BUILD_ARCHITECTURES@")
    endif()
    return()
  endif()
endforeach()

# Determine whether the user's request (either implied or explicit) for libstdc++ can
# be met by this verison of LeapHTTP
if(CMAKE_CXX_COMPILER_ID STREQUAL "AppleClang")
  if(LeapHTTP_DEBUG)
    message(STATUS "Installed LeapHTTP_USE_LIBCXX: @LeapHTTP_USE_LIBCXX@")
    message(STATUS "Configured LeapHTTP_USE_LIBCXX: ${LeapHTTP_USE_LIBCXX}")
  endif()
endif()

if(PACKAGE_VERSION VERSION_LESS PACKAGE_FIND_VERSION)
  if(LeapHTTP_DEBUG)
    message(STATUS "Our version is less than the requested one, trivially incompatible")
  endif()
  set(PACKAGE_VERSION_COMPATIBLE FALSE)
elseif(NOT PACKAGE_FIND_VERSION_MINOR EQUAL PACKAGE_VERSION_MINOR)
  if(LeapHTTP_DEBUG)
    message(STATUS "Minor version mismatch, requested minor is ${PACKAGE_FIND_VERSION_MINOR}, this version is ${PACKAGE_VERSION_MINOR}")
  endif()
  set(PACKAGE_VERSION_COMPATIBLE FALSE)
elseif(NOT PACKAGE_FIND_VERSION_MAJOR EQUAL PACKAGE_VERSION_MAJOR)
  if(LeapHTTP_DEBUG)
    message(STATUS "Major version mismatch, failure.")
  endif()
  set(PACKAGE_VERSION_COMPATIBLE FALSE)
else()
  set(PACKAGE_VERSION_COMPATIBLE TRUE)
  if (PACKAGE_VERSION VERSION_EQUAL PACKAGE_FIND_VERSION)
    set(PACKAGE_VERSION_EXACT TRUE)
  endif()

  if(LeapHTTP_DEBUG)
    message(STATUS "Using LeapHTTP in ${CMAKE_CURRENT_SOURCE_DIR}")
  endif()
endif()
